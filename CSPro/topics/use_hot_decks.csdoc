<title>Use Hot Decks</title>

Hot decks in CSPro are implemented using <link array_statement.csdoc>Array</link> objects. First, you must declare the array under the <logiccolor>PROC GLOBAL</logiccolor> section. Then you need to set the initial values for the hot deck array. There are two ways to initialize and maintain arrays in CSPro:

<list>
	<li><link initialize_hot_decks_in_program_logic.csdoc>inline</link> (in program logic), or</li>
	<li>using <link initialize_hot_decks_using_saved_arrays.csdoc>saved arrays</link>.</li>
</list>

Once the array has been declared and initialized, you need to add logic to check each occurrence of the variable you wish to impute using the hot deck. If the value of the variable is valid, update the hot deck by assigning this value to the appropriate cell in the array. If the value is invalid, set the value of the variable based on the corresponding cell in the hot deck. A simple usage could be:

<subheader>Example 1</subheader>

This example uses the traditional <link initialize_hot_decks_in_program_logic.csdoc>inline</link> method of maintaining hot decks.

<logic>
PROC GLOBAL

Array HD_Age_SexRel(2, 8);	// hot deck for age, uses sex & relationship as indices

PROC AGE

	if AGE = notappl then
		// if the value for age is invalid:
		// assign a valid value from the hot deck based on sex and relationship
		impute(AGE, HD_Age_SexRel(SEX, RELATIONSHIP));

	else
		// otherwise:
		// update the value of the hot deck with the good value found
		HD_Age_SexRel(SEX, RELATIONSHIP) = AGE;

	endif;
</logic>

<subheader>Example 2</subheader>

This example uses the <link initialize_hot_decks_using_saved_arrays.csdoc>save</link> array method of maintaining hot decks.

<logic>
PROC GLOBAL

// the hot deck for age uses the sex & relationship value sets
// to define the array dimensions

Array HD_Age_SexRel(SEX_VS, REL_VS) save;

PROC AGE

	if AGE = notappl then
		// if the value for age is invalid:
		// assign a valid value from the hot deck based on sex and relationship
		impute(AGE, HD_Age_SexRel());	// no parameters needed for HD_Age_SexRel

	else
		// otherwise:
		// update the value of the hot deck with the good value found
		HD_Age_SexRel() = AGE;	// CSPro uses the current value of sex & rel as the indices

	endif;
</logic>


When an age is missing during the data file's processing, we will use a value from the array <b>HD_Age_SexRel</b>; when an age is valid, we will "refresh" the age for the person using the current sex and relationship codes as indices into the array.

For a more detailed explanation of what hot decks are, refer to the <link https://unstats.un.org/unsd/publication/SeriesF/seriesf_82rev2e.pdf>United Nations Handbook on Population and Housing Census Edits</link>.

<seealso dynamic_imputation_hot_deck.csdoc />
