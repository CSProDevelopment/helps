<title>Map.setOnClick Function</title>


<subheader>Format</subheader>

<logicsyntax>
b = map_name.setOnClick(<arg>callbackFunction</arg>);
</logicsyntax>


<subheader>Description</subheader>

The <logiccolor>Map.setOnClick</logiccolor> function sets the function that is called when the user taps on the base map away from any markers. The <arg>callbackFunction</arg> is the name of a user function defined in the global procedure that will be called when the user taps on the map. The function name may optionally be followed by function arguments which will be passed to the function when it is run. These arguments are evaluated at the time that <logiccolor>Map.setOnClick</logiccolor> is called rather than at the time the callback is run.

To retreive the coordinates of the map location that was tapped, use the functions <logiccolor>Map.getLastClickLatitude</logiccolor> and <logiccolor>Map.getLastClickLongitude</logiccolor>.

If the user taps on a marker rather than on the map itself, <arg>callbackFunction</arg> is not called. To capture taps on markers use the <logiccolor>Map.setMarkerOnClick</logiccolor> function.


<subheader>Return Value</subheader>

The function returns a logical value of 1 (true).


<subheader>Example</subheader>

<logic>
PROC GLOBAL

// Declare a map
Map mymap;

// Declare id for map marker
numeric markerId;

// This function is called when user taps on map.
// It moves the marker to the location that the user tapped.
function mapClicked()
    numeric lat = mymap.getLastClickLatitude();
    numeric lon = mymap.getLastClickLongitude();
	mymap.setMarkerLocation(markerId, lat, lon);
end;

PROC SHOW_MAP
preproc

// Add a marker to the map at latitude 38.84839, longitude -76.931098
markerId = mymap.addMarker(38.84839, -76.931098);

// Set function that is called when the user taps on the map to move the marker
// to the new position
mymap.setOnClick(mapClicked());

// Display the map
mymap.show();
</logic>


<seealso Map.csdoc Map_setMarkerLocation_function.csdoc Map_setMarkerOnClick_function.csdoc Map_getMarkerLatitude_function.csdoc Map_getMarkerLongitude_function.csdoc />
